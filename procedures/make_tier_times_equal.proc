# This script is part of the tgutils CPrAN plugin for Praat.
# The latest version is available through CPrAN or at
# <http://cpran.net/plugins/tgutils>
#
# The tgutils plugin is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public
# License as published by the Free Software Foundation, either
# version 3 of the License, or (at your option) any later version.
#
# The tgutils plugin is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty
# of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with tgutils. If not, see <http://www.gnu.org/licenses/>.
#
# Copyright 2014-2016 Jose Joaquin Atria

#! ~~~ params
#! selection:
#!   in:
#!     textgrid: 1
#!   out:
#!     textgrid: 1
#! ~~~
#!
#! This procedure makes sure all tiers in the selected TextGrid object have
#! the same duration. This check is not normally needed, but Praat allows
#! for tiers of different durations to be merged into a single annotation
#! file, whic results in a collection of tiers with different durations.
#!
#! This is contrary to the expectations of most scripts in existence, and
#! a source of hard to predict errors when found. Since it is also hard
#! to check whether a given TextGrid will suffer from this, this script
#! extends all tiers of insufficient length until they reach the duration
#! of the longest.
#!
#! The procedure is idempotent, so calling it on a regular TextGrid object
#! has no ill effects.
#!
procedure makeTierTimesEqual ()
  .id       = selected("TextGrid")
  .name$    = selected$("TextGrid")
  .duration = Get total duration
  .change   = 0

  .tiers = Get number of tiers
  for .t to .tiers
    selectObject: .id
    .tier[.t] = Extract one tier: .t
    .tier_duration[.t] = Get total duration
    if .tier_duration[.t] < .duration
      .change = 1
    endif
    # End tier loop
  endfor

  if .change
    for .t to .tiers
      if .tier_duration[.t] < .duration
        .delta = .duration - .tier_duration[.t]
        Extend time: .delta, "End"
      endif
      # End tier loop
    endfor

    nocheck selectObject: undefined
    for .t to .tiers
      plusObject: .tier[.t]
    endfor

    .id =  Merge
    Rename: .name$ + "_equalised"
  endif

  for .t to .tiers
    removeObject: .tier[.t]
  endfor

  selectObject: .id
endproc
