# This script is part of the tgutils CPrAN plugin for Praat.
# The latest version is available through CPrAN or at
# <http://cpran.net/plugins/tgutils>
#
# The tgutils plugin is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public
# License as published by the Free Software Foundation, either
# version 3 of the License, or (at your option) any later version.
#
# The tgutils plugin is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty
# of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with tgutils. If not, see <http://www.gnu.org/licenses/>.
#
# Copyright 2016 Jose Joaquin Atria

#! ~~~ params
#! in:
#!   .tier: >
#!     The tier number to process
#! selection:
#!   in:
#!     textgrid: 1
#! ~~~
#!
#! This procedure will look for contiguous matching interval labels and
#! merge them into a single interval, keeping that original label.
#!
procedure mergeContiguousLabels: .tier
  .textgrid = selected("TextGrid")
  .total_intervals = Get number of intervals: .tier
  .interval = Is interval tier: .tier

  if !.interval
    exitScript: "Tier ", .tier, " is not an interval tier"
  endif

  # Make sure no two contiguous intervals have the same labels
  for .j from 0 to .total_intervals - 2
    .current  = .total_intervals - .j
    .previous = .current - 1

    .current$  = Get label of interval: 1, .current
    .previous$ = Get label of interval: 1, .previous

    if .current$ == .previous$
      if .interval
        Remove left boundary: .tier, .current
        Set interval text: .tier, .previous, .current$
      else
        # Point tier not yet implemented
      endif
    endif
  endfor
endproc
